plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    Properties localProperties = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    }

    splits {
        abi {
            enable = true
            reset()
            include 'arm64-v8a', 'x86_64', 'armeabi-v7a', 'x86'
            universalApk = true
        }
    }

    signingConfigs {
        sign {
            storeFile file(signStoreFile)
            storePassword signStorePassword
            keyAlias signkeyAlias
            keyPassword signkeyPassword
        }
    }

    defaultConfig {
        applicationId "org.moedog.ehviewer"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 114530
        versionName "1.7.27.8"
        resConfigs 'zh', 'zh-rCN', 'zh-rHK', 'zh-rTW', 'es', 'ja', 'ko', 'fr', 'de', 'th'
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lint {
        abortOnError true
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/**"
            excludes += "/kotlin/**"
            excludes += "**.txt"
            excludes += "**.bin"
        }
    }

    dependenciesInfo.includeInApk false

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/java-gen'
        }
    }

    namespace 'com.hippo.ehviewer'
}

tasks.withType(JavaCompile) {
    task -> task.dependsOn ":daogenerator:executeDaoGenerator"
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'

    implementation 'androidx.activity:activity:1.6.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core:1.9.0'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.1.0-alpha03'
    implementation 'androidx.fragment:fragment:1.5.4'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'androidx.webkit:webkit:1.5.0'
    implementation 'com.drakeet.drawer:drawer:1.0.3'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.3'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.10'
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:5.0.0-alpha.10'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'dev.rikka.rikkax.appcompat:appcompat:1.5.0.1'
    implementation 'dev.rikka.rikkax.core:core:1.4.1'
    implementation 'dev.rikka.rikkax.insets:insets:1.3.0'
    implementation 'dev.rikka.rikkax.layoutinflater:layoutinflater:1.2.0'
    //noinspection GradleDependency
    implementation 'dev.rikka.rikkax.material:material:1.6.6'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-ktx:1.3.1'
    implementation 'dev.rikka.rikkax.widget:borderview:1.1.0'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    implementation 'org.greenrobot:greendao:3.3.0'
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation projects.native
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}
